# Process this file with autoconf to produce a configure script.
AC_INIT([mbfl/mbfilter.h])
AM_INIT_AUTOMAKE([libmbfl], [1.1.0])
AC_CONFIG_SRCDIR([mbfl/mbfilter.c])
AC_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

# Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_LIBTOOL
AC_PROG_RANLIB

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h stddef.h assert.h errno.h])

AC_ARG_ENABLE([threads],
  [  --enable-threads    enable threading support],
  [enable_threads="$enableval"],
  [enable_threads="autodetect"])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_FUNC_MEMCMP
AC_CHECK_FUNCS([strcasecmp strchr memmove])

if test "$enable_threads" != "no"; then
  MBFL_PTHREADS([
    avail_pthread="yes"
  ],[
    avail_pthread="no"
  ])

  MBFL_WIN32_NATIVE_THREADS([
    avail_win32_native_thread="yes"
  ],[
    avail_win32_native_thread="no"
  ])

  MBFL_GNUPTH([
    avail_gnupth="yes"
  ],[
    avail_gnupth="no"
  ])

  if test "$enable_threads" == "autodetect"; then
    enable_threads="no"
    if test "$avail_pthread" != "no"; then
      enable_threads="pthread"
    fi
    if test "$avail_gnupth" != "no"; then
      enable_threads="gnupth"
    fi
  fi
fi

if test "$enable_threads" != "no"; then
  AC_DEFINE([ENABLE_THREADS],[1],[Define to 1 if threading support is enabled])
# Thread configuration stolen from mono project
  case "$host" in
    *-*-mingw*|*-*-cygwin*)
      CPPFLAGS="$CPPFLAGS" 
      ;;

    *-*-*bsd*)
      CPPFLAGS="$CPPFLAGS -D_THREAD_SAFE"
      ;;

    *-*-linux*)
      CPPFLAGS="$CPPFLAGS -D_REENTRANT"
      ;;
    *-*-solaris*)
      CPPFLAGS="$CPPFLAGS -D_REENTRANT"
      ;;
  esac

  case "$enable_threads" in
    "pthread" [)]
      AC_DEFINE([USE_PTHREAD],[1],[Define to 1 if pthread is adopted for threading])
      ;;
    "win32native" [)]
      AC_DEFINE([USE_WIN32_NATIVE_THREAD],[1],[Define to 1 if win32 native thread is adopted for threading])
    "gnupth" [)]
      AC_DEFINE([USE_GNUPTH],[1],[Define to 1 if GNUPth is adopted for threading])
  esac
fi


AC_CONFIG_FILES([Makefile mbfl/Makefile filters/Makefile nls/Makefile])
AC_OUTPUT
